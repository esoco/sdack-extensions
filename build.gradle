buildscript {
    repositories { jcenter() }
    dependencies { classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6' }
}
apply plugin: 'gwt-base'

apply plugin: 'java'
apply plugin: 'osgi'

project.group   = 'de.esoco'
project.version = '1.1.0'

sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
 	if (project(':sdack').getProjectDir().exists()) {
		compile project(':sdack')
	}
	else {
		compile 'de.esoco:sdack:1.1.+'
	}
 	if (project(':gewt-chart').getProjectDir().exists()) {
		compile project(':gewt-chart')
	}
	else {
		compile 'de.esoco:gewt-chart:1.0.+'
	}
 	if (project(':gewt-timetable').getProjectDir().exists()) {
		compile project(':gewt-timetable')
	}
	else {
		compile 'de.esoco:gewt-timetable:1.0.+'
	}
 	if (project(':esoco-oauth').getProjectDir().exists()) {
		compile project(':esoco-oauth')
	}
	else {
		compile 'de.esoco:esoco-oauth:1.0.+'
	}

	compile 'javax.servlet:javax.servlet-api:3.0.1'
	compile 'commons-fileupload:commons-fileupload:1.3.+'

    testCompile 'junit:junit:4.+'
}

gwt {
    gwtVersion='2.7.0'
}

// sources are need for the GWT compiler
jar { from sourceSets.main.allSource }

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

task fullJar(type: Jar) {
    baseName = 'sdack-extensions-all'

    manifest {
        attributes 'Implementation-Title': 'SDACK - Simple Development Application Component Kit',
	        	   'Implementation-Version': project.version,
	               'Built-Date': new Date(),
                   'Built-JDK': System.getProperty('java.version')

    }
    // exclude files from GWT jar
 	exclude('com/**')
 	exclude('javax/**')
 	exclude('rhino_ast/**')
 	exclude('META-INF/maven/**')
 	// exclude hibernate which is only used for testing
 	exclude('org/hibernate/**')

	from { configurations.compile.collect
		{ it.isDirectory() ? it : zipTree(it) }
	}
    with jar
}
